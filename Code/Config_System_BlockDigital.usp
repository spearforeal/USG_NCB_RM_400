#DEFINE_CONSTANT MAX_PROPERTIES 32
#CATEGORY "46" "Vault"
#DEFAULT_VOLATILE
#ENABLE_STACK_CHECKING
#ENCODING_ASCII
#USER_SIMPLSHARP_LIBRARY "ConfigSystem"
DIGITAL_INPUT  _SKIP_, _SKIP_, SetOn[MAX_PROPERTIES], _SKIP_,
SetOff[MAX_PROPERTIES], _SKIP_, Toggle[MAX_PROPERTIES, MAX_PROPERTIES];
DIGITAL_OUTPUT _SKIP_, _SKIP_, OnFB[MAX_PROPERTIES], _SKIP_, 
OffFB[MAX_PROPERTIES, MAX_PROPERTIES];
STRING_PARAMETER ManagerName[50], ValueName[50];
#BEGIN_PARAMETER_PROPERTIES ManagerName
   propValidUnits = unitString;
   propDefaultUnit = unitString;
   propDefaultValue = "SystemConfig";
   propShortDescription = "Name of the Config File Manager.";
#END_PARAMETER_PROPERTIES

#BEGIN_PARAMETER_PROPERTIES ValueName
   propValidUnits = unitString;
   propDefaultUnit = unitString;
   propDefaultValue = "GroupDigital";
   propShortDescription = "Name of this setting.";
#END_PARAMETER_PROPERTIES

ConfigValue theConfig;
FUNCTION ModuleInitialize()
{ 
	theConfig.Register(ManagerName, ValueName, 1);

	RegisterEvent(theConfig, OnValueChanged, onValueChangedHandler);
}
PUSH SetOn
{
	theConfig.SetBlockDigital(GetLastModifiedArrayIndex(), 1);
}
 
PUSH SetOff
{
	theConfig.SetBlockDigital(GetLastModifiedArrayIndex(), 0);
}

PUSH Toggle
{
  theConfig.ToggleBlockDigital(GetLastModifiedArrayIndex());
}

EVENTHANDLER onValueChangedHandler(ConfigValue sender, ConfigEventArgs args)
{
    OnFB[args.ValueIndex] = args.ValueAsDigital();
    OffFB[args.ValueIndex] = !args.ValueAsDigital();
}
Function Main()
{
    WaitForInitializationComplete();

    ModuleInitialize();

} 
